<!doctype html>
<html>
<head>
    <title>Cube Pull</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">

    <link rel="stylesheet" type="text/css" href="style.css">

    <script src="thirdparty/gl-matrix-min.js"  type="text/javascript"></script>

    <script src="js/time.js"           type="text/javascript"></script>
    <script src="js/texture.js"        type="text/javascript"></script>
    <script src="js/mesh.js"           type="text/javascript"></script>
    <script src="js/framebuffer.js"    type="text/javascript"></script>
    <script src="js/material.js"       type="text/javascript"></script>
    <script src="js/batched-cubes.js"  type="text/javascript"></script>
    <script src="js/pullcube.js" type="text/javascript"></script>
    <script src="js/app.js" type="text/javascript"></script>

    <!-- Fragment shader program -->

    <script id="cubeframe-fs" type="x-shader/x-fragment">
        varying highp vec3 vObjNormal;


        void main(void) {
            highp vec4 texelColor;
                      
            bool onEdgeX = ( vObjNormal.x > 0.5 && vObjNormal.x < 0.9 ) || ( vObjNormal.x < -0.5 && vObjNormal.x > -0.9 );
            bool onEdgeY = ( vObjNormal.y > 0.5 && vObjNormal.y < 0.9 ) || ( vObjNormal.y < -0.5 && vObjNormal.y > -0.9 );
            bool onEdgeZ = ( vObjNormal.z > 0.5 && vObjNormal.z < 0.9 ) || ( vObjNormal.z < -0.5 && vObjNormal.z > -0.9 );
            
            if(onEdgeX && onEdgeZ || onEdgeX && onEdgeY || onEdgeY && onEdgeZ )
            {
                texelColor.rgb = vec3(0.0);
                texelColor.a = 1.0;
            }
            else
            {
                discard;
            }
            
            gl_FragColor = texelColor;
        }
    </script>
    
    <script id="cornercolor-fs" type="x-shader/x-fragment">
        varying highp vec3 vObjNormal;

        void main(void) {
            highp vec4 texelColor;

            texelColor.rgb = ( clamp( vObjNormal.xyz * 2.0, vec3(-1.0), vec3(1.0)) + 1.0 ); //( clamp( vObjNormal.xyz * 2.0, vec3(-1.0), vec3(1.0)) + 1.0 ) * 0.5;
            texelColor.a = 1.0;
                        
            
            gl_FragColor = texelColor;
        }
    </script>
    
    <script id="basic-vs" type="x-shader/x-vertex">       
        attribute highp vec3 aVertexPosition;

        uniform highp mat4 uNormalMatrix;
        uniform highp mat4 uMMatrix;
        uniform highp mat4 uVMatrix;
        uniform highp mat4 uPMatrix;
        uniform sampler2D uPosTex;

        uniform highp float uCubeSize;       // 64.0
        uniform highp float uLayersPerRow;   // 8
        uniform highp float uImageSize;      // 512

        varying highp vec3 vObjNormal;

        vec2 uvFromCube(highp vec3 cubePos )
        {
            return vec2( mod( cubePos.x, uCubeSize ), mod( cubePos.y, uCubeSize ) ) + vec2(mod(cubePos.z, uLayersPerRow), floor(cubePos.z / uLayersPerRow) ) * uCubeSize;
        }

        void main(void) {

            highp vec3 cubePos = aVertexPosition + vec3(0.5, 0.5, 0.5 );
            cubePos.xyz = floor( cubePos.xyz / 2.0 );

            highp vec3 offset = aVertexPosition - cubePos * 2.0;      
            
            highp vec2 texUV = uvFromCube(cubePos); // + vec3(0.0, offset.y, 0.0));

            texUV = texUV / uImageSize;
            highp vec4 pos = texture2D(uPosTex, texUV ).rgba;
            
            
            
            highp vec2 rotPos = normalize( vec2( pos.x, pos.z ) );
            
            // rotate around y axis based on xz position
            /*highp mat4 yRot;
            yRot[0] = vec4(rotPos.x, 0.0, rotPos.y, 0.0);
            yRot[1] = vec4(0.0, 1.0, 0.0, 0.0 );
            yRot[2] = vec4(-rotPos.y, 0.0, rotPos.x, 0.0 );
            yRot[3] = vec4( 0.0, 0.0, 0.0, 1.0 );
            
            offset = ( yRot * vec4(offset, 1.0) ).xyz; */
            
            pos.xyz = offset + pos.xyz;
            
            gl_Position = uPMatrix * uVMatrix * uMMatrix * vec4(pos.xyz, 1.0);

            highp vec3 objNormal = normalize((cubePos - uCubeSize));

            vObjNormal = objNormal;
        }
    </script>
    
    <script id="litoutline-fs" type="x-shader/x-fragment">
        varying highp vec3 vNormal;
        varying highp vec3 vObjNormal;

        void main(void) {
            highp vec4 texelColor = vec4(1.0, 7.0, 1.0, 1.0);
            
            bool onEdgeX = ( vObjNormal.x > 0.5 && vObjNormal.x < 0.9 ) || ( vObjNormal.x < -0.5 && vObjNormal.x > -0.9 );
            bool onEdgeY = ( vObjNormal.y > 0.5 && vObjNormal.y < 0.9 ) || ( vObjNormal.y < -0.5 && vObjNormal.y > -0.9 );
            bool onEdgeZ = ( vObjNormal.z > 0.5 && vObjNormal.z < 0.9 ) || ( vObjNormal.z < -0.5 && vObjNormal.z > -0.9 );
            
            if(onEdgeX && onEdgeZ || onEdgeX && onEdgeY || onEdgeY && onEdgeZ )
            {
                texelColor.rgb = vec3(0.0, 0.3, 0.0);
            }
            else
            {
                // Apply lighting effect
                highp vec3 ambientLight = vec3(0.3, 0.3, 0.3);
                highp vec3 directionalLightColor = vec3(0.6, 0.6, 0.6);
                highp vec3 directionalVector = vec3(0.85, 0.8, 0.75);
                
                highp float directional = (dot(vNormal.xyz, directionalVector) + 1.0) * 0.5;
                highp vec3 lighting = ambientLight + (directionalLightColor * directional);    
                texelColor.rgb *= lighting;
            }       
            
            gl_FragColor = texelColor;
        }
    </script>

    <script id="litoutline-vs" type="x-shader/x-vertex">       
        attribute highp vec3 aVertexPosition;

        uniform highp mat4 uNormalMatrix;
        uniform highp mat4 uMVMatrix;
        uniform highp mat4 uPMatrix;
        uniform sampler2D uPosTex;

        varying highp vec3 vNormal;
        varying highp vec3 vObjNormal;

        void main(void) {

            highp vec3 cubePos = aVertexPosition + vec3(1.0, 0.0, 1.0 );
            cubePos.xz = floor( cubePos.xz / 3.0 );
            cubePos.y = 0.0;

            highp vec4 pos = texture2D(uPosTex, cubePos.xz / 512.0 ).rgba;
            
            highp vec3 offset = aVertexPosition - cubePos * 3.0;
            
            highp vec2 rotPos = normalize( vec2( pos.x, pos.z ) );
            
            // rotate around y axis based on xz position
            highp mat4 yRot;
            yRot[0] = vec4(rotPos.x, 0.0, rotPos.y, 0.0);
            yRot[1] = vec4(0.0, 1.0, 0.0, 0.0 );
            yRot[2] = vec4(-rotPos.y, 0.0, rotPos.x, 0.0 );
            yRot[3] = vec4( 0.0, 0.0, 0.0, 1.0 );
            
            offset = ( yRot * vec4(offset, 1.0) ).xyz;
            
            pos.xyz = offset + pos.xyz;
            
            gl_Position = uPMatrix * uMVMatrix * vec4(pos.xyz, 1.0);

            highp mat4 normalMat = yRot * uNormalMatrix;
            highp vec3 objNormal = normalize(aVertexPosition - cubePos * 3.0);
            highp vec4 transformedNormal = normalMat * vec4(objNormal, 1.0);
            
            vNormal = transformedNormal.xyz;
            vObjNormal = objNormal;
        }
    </script>

    <script id="screenquad-vs" type="x-shader/x-vertex">
        attribute highp vec3 aVertexPosition;

        varying highp vec2 vTextureCoord;

        void main(void)
        {
            gl_Position = vec4(aVertexPosition.xy, 1.0, 1.0);
            vTextureCoord = (aVertexPosition.xy + vec2(1.0)) * 0.5;
        }
    </script>

    <script id="velocity-fs" type="x-shader/x-fragment">
        varying highp vec2 vTextureCoord;


        uniform sampler2D uVelTex;
        uniform sampler2D uPosTex;
        uniform sampler2D uDesiredPosTex;

        uniform highp mat4 uMMatrix;
        uniform highp mat4 uVMatrix;
        uniform highp mat4 uPMatrix;

        uniform highp mat4 uInvMVPMatrix;

        uniform highp float uDeltaTime;
        uniform highp float uRandomness;
        uniform highp float uTime;
        uniform highp float uGravityScale;

        uniform highp float uImageSize;

        uniform highp float uAspect;

        uniform highp float uRadius;
        uniform highp vec3 uMousePos;

        highp float rand(highp vec2 co){
            return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
        }

        void main(void) {
            highp vec4 pos = texture2D(uPosTex, vTextureCoord.st);
            highp vec4 vel = texture2D(uVelTex, vTextureCoord.st);

            highp vec4 desPos = texture2D(uDesiredPosTex, vTextureCoord.st);

            highp vec4 scrPos = uPMatrix * uVMatrix * uMMatrix * vec4(pos.xyz, 1.0);

            scrPos.xyz /= scrPos.w;
            scrPos.xy = scrPos.xy * 0.5 + 0.5;

            highp vec2 off = scrPos.xy - uMousePos.xy;

            highp float sqDist = dot(off, off);
            highp float sqRad = uRadius * uRadius;

            highp vec2 scrVel = vec2(0.0, 0.0);


            {
                scrVel.xy = -(off * 5.0 / max(sqDist, 0.0001))  * uDeltaTime;
            }

            highp vec4 wVel = (uInvMVPMatrix * vec4(scrVel.xy, 0.0, 0.0));

            vel.xyz += wVel.xyz;

            highp vec3 adjOff =  desPos.xyz - pos.xyz;
            highp float adjDist =  length(adjOff);

            adjDist = adjDist*adjDist * 0.05;
            vel.xyz += adjOff * adjDist * uDeltaTime;

            
            vel.xyz = vel.xyz - vel.xyz * uDeltaTime * 0.3;
            
            //vel.xy += scrPos.xy;

            gl_FragColor = vel;
        }
    </script>

    <script id="position-fs" type="x-shader/x-fragment">
        varying highp vec2 vTextureCoord;

        uniform sampler2D uVelTex;
        uniform sampler2D uPosTex;

        uniform highp float uDeltaTime;
        
        highp float rand(highp vec2 co){
            return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
        }

        void main(void) {
            highp vec4 pos = texture2D(uPosTex, vTextureCoord.st);
            highp vec4 vel = texture2D(uVelTex, vTextureCoord.st);

            pos.xyz = pos.xyz + vel.xyz * uDeltaTime;
            pos.a = 1.0;

            gl_FragColor = pos;
        }
    </script>

    <script id="compose-fs" type="x-shader/x-fragment">
        varying highp vec2 vTextureCoord;

        uniform sampler2D uScrTex;
        uniform sampler2D uPosTex;
        uniform sampler2D uVelTex;

        void main(void) {
            highp vec4 col = texture2D(uScrTex, vTextureCoord.st);
            gl_FragColor = col;
        }
    </script>

    <script id="copy-fs" type="x-shader/x-fragment">
        varying highp vec2 vTextureCoord;

        uniform sampler2D uCopyTex;

        void main(void) {
            highp vec4 col = texture2D(uCopyTex, vTextureCoord.st);
            gl_FragColor = col;
        }
    </script>

    <script id="initdata-fs" type="x-shader/x-fragment">
        varying highp vec2 vTextureCoord;

        uniform highp float uCubeSize;       // 64.0
        uniform highp float uLayersPerRow;   // 8
        uniform highp float uImageSize;      // 512

        highp float rand(highp vec2 co){
            return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
        }

        void main(void) {
            highp float spacing = 1.0/uLayersPerRow;
            
            highp vec3 cubePos;        
            cubePos.x = mod( vTextureCoord.x, spacing ) * uLayersPerRow;
            cubePos.y = mod( vTextureCoord.y, spacing ) * uLayersPerRow;
            cubePos.z = floor( vTextureCoord.x * uLayersPerRow ) + floor( vTextureCoord.y * uLayersPerRow ) * uLayersPerRow;  
            
            cubePos.xy *= uCubeSize;
            
            cubePos.xyz = floor(cubePos.xyz);
            
            cubePos.xyz -= vec3(uCubeSize*0.5);
            

            highp vec4 col = vec4( cubePos * 4.0, 1.0 );
            gl_FragColor = col;
        }
    </script>
</head>

<body onload="start()" style="margin: 0px;">
    <div class="container">
        <canvas id="glcanvas" width="1200" height="480" style="width:100%;height:100%;">
            Your browser doesn't appear to support the <code>&lt;canvas&gt;</code> element.
        </canvas>
        <!-- <div id="overlay">
            <div>Cube Count: <span id="cubeCount"></span></div>
            <div><input type="range" id="cubeSlider" value="100" ></div>
            
            <div>Randomness: <span id="randomness"></span></div>
            <div><input type="range" id="randomSlider" value="0" ></div>
            
            <div>Gravity: <span id="gravity"></span></div>
            <div><input type="range" id="gravitySlider" value="0" min="0" max="50"></div>
            
            <div>Time Scale: <span id="timeScale"></span></div>
            <div><input type="range" id="timeSlider" value="100" min="20" max="100"></div>
            <div><button id="shaderButton">Change Shader</button></div>
        </div> -->
    </div>

</body>
</html>